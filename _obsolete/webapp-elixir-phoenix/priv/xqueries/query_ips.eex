xquery version "3.1";

declare variable $tenant external;
declare variable $wantedBegin external;
declare variable $wantedEnd external;

for $record in collection('/dmarc/' || $tenant)/feedback/record
    let $metadata := $record/ancestor::feedback/report_metadata
    let $reportId := $metadata/report_id/text()
    let $reportOrg := $metadata/org_name/text()
    let $reportEmail := $metadata/email/text()
    let $reportBegin := $metadata/date_range/begin/text()
    let $reportEnd := $metadata/date_range/end/text()
    where $reportBegin <= $wantedEnd and $reportEnd >= $wantedBegin (: i.e. inclusive/lax :)
    (:  where $reportBegin >= $wantedBegin and $reportEnd <= $wantedEnd :) (: i.e. exclusive/strict :)
    let $row := $record/row
    let $ip := $row/source_ip/text()
    let $spf := $row/policy_evaluated/spf/text()
    let $dkim := $row/policy_evaluated/dkim/text()
    let $dmarc := $spf = "pass" or $dkim = "pass"
    let $count := $row/count/text()
    group by $dmarc, $ip
    order by $dmarc, $ip
    return
        <item>
            <source_ip>{$ip}</source_ip>
            <dmarc>{if ($dmarc) then "pass" else "fail"}</dmarc>
            <count>{sum($count)}</count>
            <reports>
                {for $id at $pos in $reportId return
                    <report>
                        <id>{$id}</id>
                        <org>{$reportOrg[$pos]}</org>
                        <email>{$reportEmail[$pos]}</email>
                    </report>
                }
            </reports>
        </item>

